#!/usr/bin/env perl

=pod

=head1 NAME

dmaster - Manage continuously running processes

=head1 SYNOPSIS

dmaster

=head1 DESCRIPTION

Daemon master.
Start programs defined in B<daemontab> file.
Restart them when ended.

Re-read the B<daemontab> file on B<HUP> signal,
stops processes which are no longer in it (or I<ID> changed) with SIGTERM signal,
and starts new ones.
Those daemons whose I<COMMAND> I<ARGS> changed, are not restarted automatically.

Automatically respawn a daemon when it exits.
If a daemon exits too often, suppress it a little while.
Default parameters: at most 5 spawns in 2 sec, hold up for 10 sec.
Therefore commands defined in B<daemontab> are not supposed to fork out a "real" daemon process,
just run in "foreground".
Well, it's called foreground in most service's terminology, but they will be as much in the background as dmaster(8) is.

=head1 FILES

=over 4

=item F</etc/daemontab>

Format is I<ID> I<COMMAND> I<ARGS>, delimited by any amount of space/tab, one line per each.
Lines starting with hash mark (C<#>) are ignored as usual.
I<ID> is an arbitrary word choosed by the user, to identify daemon commands across reloads.
Command arguments may enclosed in double quotes (C<">), but not part of the argument. See:

 d1 my-daemon --param="foo bar"

is invalid (parsed as 2 parameters: C<--param="foo> and C<bar"> with dangling quotes).

 d1 my-daemon "--param=foo bar"

is what you want.

It's not recommended to write complex scripts directly in daemontab.
Put them in F</etc/daemons> directory instead and call over to them like:

 my-service /etc/daemons/my-service.sh

=item F</var/run/dmaster.sock>

dmaster(1) listens on a Unix Domain socket (see unix(7)).
When a client connects, internal state is dumped to the socket
in B<Single Value per Line with Hierarchical Keys> format.
Example:

 dmaster.pid	4363
 daemon.custom-service.cmd_args.0	setpgrp
 daemon.custom-service.cmd_args.1	/etc/daemons/my-custom-service.sh
 daemon.custom-service.cmd_args.2	--no-fork
 daemon.custom-service.pid	26300
 daemon.custom-service.pidfd	Linux::FD::Pid
 daemon.custom-service.pidfd.fileno	5
 daemon.custom-service.starttimes.0	1761620826
 daemon.custom-service.starttimes.1	1761620836
 daemon.custom-service.exittimes.0	1761620836
 daemon.custom-service.exitcode	2
 daemon.custom-service.exitsignal	
 END

Fields:

=over 4

=item B<dmaster.pid> and B<< daemon.I<DAEMON-ID>.pid >>

PID of the dmaster process itself and of individual daemons (identified by I<DAEMON-ID>).
Note: the OS may reuse PIDs after a process terminates,
which possibly happen after dmaster dump but before the system admin, or a system automation, does something with the PID, eg. send signal to it by kill(1).
So sending signals to a previously reported PID may affect an unrelated process.
To alleviate this condition,
internal state includes all started daemon's B<PIDFD>,
which may be copied by a system admin program by pidfd_getfd(2),
in order to reliably send signals to it.
See dmaster-signal(8).

=item B<.cmd_args>

List of the command name and its arguments.
Each argument as a separate item on the list.

=item B<< .starttimes.I<N> >> and B<< .exittimes.I<N> >>

List of unix epoch timestamps when the daemon started and exited
upto B<DAEMONS_RESPAWN_BURST_LIMIT> occurrances.

=item B<.exitcode> and B<.exitsignal>

Last exit status code of the daemon process
and the signal number if it was terminated by a signal.

=back

After the internal state dump is closed by a line containing a lone C<END>,
it reads one line on the management socket.
Currently you can not control dmaster(1) this way (use signals for this, see L</SIGNALS>),
but while it waits for that one line, does not do anything,
so it's time for the management client side to call pidfd_getfd(2)
to reliably copy the PIDFD representing a given daemon process.

B<Note>, this also means that daemon management is paused until
normal operation is resumed by sending a newline on the management socket.
Due to this potential local-DoS vector, management socket is chmod(2) to 0660.

=item F</var/run/shm/dmaster.state>

Dump internal state to it.
See USR1 signal below.

=back

=head1 SIGNALS

=over 4

=item USR1

Write internal state into F</var/run/shm/dmaster.state> file.

=item TERM, INT

Terminate managed daemons, always with one B<TERM> signal each,
wait for all of them to exit, then exit dmaster(1) itself.
If a daemon is a process group leader (become on its own or started via setpgrp(1)), sends the signal to the whole process group at once.

=item HUP

Re-read B<daemontab>, stop daemons which are disappeared and start new ones.
Leave others running.

=back

=head1 ENVIRONMENT

=over 4

=item XDG_CONFIG_HOME

Where to find C<daemontab>.
Default is F</etc>.

=item XDG_CACHE_HOME

Where to create F<dmaster.sock>
Default is F</var/run>.

=item DAEMONS_RESPAWN_BURST_PERIOD

Measure this much time (seconds) to detect respawn-burst.

=item DAEMONS_RESPAWN_BURST_LIMIT

If a daemon respawns this many times within DAEMONS_RESPAWN_BURST_PERIOD,
consider it a respawn-burst, and hold it back a while.

=item DAEMONS_RESPAWN_BURST_HOLD

How much time (seconds) to wait before starting a daemon
after a respawn-burst.

=back

=head1 EXAMPLE INSTALLATION

Put this in your inittab(5):

 dm:2345:respawn:/sbin/dmaster

To send logs to syslog:

 dm:2345:respawn:stdsyslog -f daemon /sbin/dmaster
 dm:2345:respawn:loggerexec daemon dmaster /sbin/dmaster

To directly save to per-day log files:

 dm:2345:respawn:saveout --out /var/log/dmaster/%F.log --flush-sec=1 /sbin/dmaster

Remove the control socket in case of it's left there:
(first involves shell, second does not)

 dm:2345:respawn:rm /var/run/dmaster.sock; exec /sbin/dmaster
 dm:2345:respawn:multicmd -d -- rm /var/run/dmaster.sock -- /sbin/dmaster

May use the default (although a bit confusing) semicolon multicmd(1) delimiter while still not involving shell,
if your init(1) supports the "at" mark (C<@>) syntax:

 dm:2345:respawn:@multicmd rm /var/run/dmaster.sock ; loggerexec daemon dmaster dmaster

Don't forget to place this inittab(5) entry above any entry itself with a B<wait> action.

=head1 SEE ALSO

dmaster-signal(8), daemontab(5),
supervisor(1), supervise(8), daemon(1), runit(1), L<sinit|https://core.suckless.org/sinit/>,
setpgrp(1), multicmd(1), saveout(1)

=cut


use Data::Dumper;
use POSIX ":sys_wait_h";
no if ($] >= 5.018), 'warnings' => 'experimental::smartmatch';
use Socket;
use Config;
require 'syscall.ph';
@signal_names = split ' ', $Config{'sig_name'};

$pidfd_support = eval q{ use Linux::FD::Pid; 1; };
warn "deprecating PIDFD support: $@" if $@;


$0 = 'dmaster';
%Daemon = ();
$respawn_burst_period = $ENV{'DAEMONS_RESPAWN_BURST_PERIOD'} // 2;
$respawn_burst_limit =  $ENV{'DAEMONS_RESPAWN_BURST_LIMIT'} // 5;
$respawn_burst_hold =   $ENV{'DAEMONS_RESPAWN_BURST_HOLD'} // 10;


sub logger
{
	my $s = shift;
	warn "$0: $s\n";
}

sub append_fixsizelist
{
	my ($listref, $size, @items) = @_;
	push @$listref, @items;
	while(scalar @$listref > $size)
	{
		shift @$listref;
	}
}

sub getpgid
{
	my $pid = shift;
	return syscall(SYS_getpgid(), int $pid);
}

sub stop_daemon
{
	my $did = shift;
	my $pid = $Daemon{$did}->{'pid'};
	if(defined $pid)
	{
		my $pgid = getpgid($pid);
		if($pgid == $pid)
		{
			logger "daemon $did is a process group leader, terminating PGID $pgid";
			$kid = -$pid;
		}
		else
		{
			logger "terminating $did PID $pid";
			$kid = $pid;
		}
		kill 'TERM', $kid;
	}
	else
	{
		logger "daemon $did has no PID";
	}
}

sub start_daemon
{
	my $id = shift;
	my $cmd_args = $Daemon{$id}->{'cmd_args'};
	logger "starting $id: @$cmd_args";
	
	my $pid = fork;
	if($pid == 0)
	{
		close $management_socket;
		close $_ for map {$Daemon->{$_}->{'pidfd'}} grep {exists $Daemon->{$_}->{'pidfd'}} keys %Daemon;
		# leave STDIO there for convenience
		
		exec @$cmd_args;
		exit 127;
	}
	elsif(not defined $pid)
	{
		logger "fork: $!";
	}
	else
	{
		logger "started $id PID $pid";
		$Daemon{$id}->{'pid'} = $pid;
		$Daemon{$id}->{'pidfd'} = Linux::FD::Pid->new($pid) if $pidfd_support;
		append_fixsizelist \@{$Daemon{$id}->{'starttimes'}}, $respawn_burst_limit, time;
	}
}

sub parse_cmd_string
{
	my $cmd = shift;
	my @cmd_args = ();
	while($cmd =~ /^("([^""]*)"|\S+)\s*/)
	{
		my $arg = defined $2 ? $2 : $1;
		push @cmd_args, $arg;
		$cmd = $';
	}
	return @cmd_args;
}

sub read_daemon_inittab
{
	my $path = shift;
	# TODO: lock
	open my $fh, '<', $path or logger "open $path: $!";
	return unless $fh;
	my %new_daemon = ();
	while(<$fh>)
	{
		next if /^\s*(#|$)/;
		if(my ($id, $cmd) = /^(\S+)\s+(.+)$/)
		{
			my @cmd_args = parse_cmd_string $cmd;
			$new_daemon{$id}->{'cmd_args'} = \@cmd_args;
		}
		else
		{
			logger "can not parse line $.";
		}
	}
	close $fh;
	return %new_daemon;
}

sub reload
{
	my $daemontab = ($ENV{'XDG_CONFIG_HOME'} || '/etc').'/daemontab';
	logger "reloading $daemontab";
	my %new_daemon = read_daemon_inittab $daemontab;
	
	for my $id (keys %Daemon)
	{
		if($id ~~ [keys %new_daemon])
		{
			logger "keep $id";
			$Daemon{$id}->{'cmd_args'} = $new_daemon{$id}->{'cmd_args'};
			delete $new_daemon{$id};
		}
		else
		{
			logger "terminate $id as it's no longer managed";
			delete $Daemon{$id}->{'cmd_args'};
			stop_daemon $id;
		}
	}
	
	for my $id (keys %new_daemon)
	{
		$Daemon{$id}->{'cmd_args'} = $new_daemon{$id}->{'cmd_args'};
	}
	for my $id (keys %new_daemon)
	{
		start_daemon $id;
	}
}

sub errstr
{
	my $errno = shift;
	$! = $errno;
	return $^E;
}

sub handle_children
{
	while(1)
	{
		my $reaped_pid = waitpid(-1, WNOHANG);  # check if any children terminated
		last if $reaped_pid <= 0;  # no more zombie children
		my $status = ${^CHILD_ERROR_NATIVE};
		my $exitcode = WIFEXITED($status) ? WEXITSTATUS($status) : undef;
		my $exitsignal = WIFSIGNALED($status) ? WTERMSIG($status) : undef;
		my $reaped = 0;
		
		for my $id (keys %Daemon)
		{
			my $daemon_pid = $Daemon{$id}->{'pid'};
			next if not defined $daemon_pid;
			next if $daemon_pid != $reaped_pid;
			
			logger sprintf "daemon %s PID %d deceased, exit status %s (%s) signal %s (%s)",
				$id, $daemon_pid, $exitcode // 'none', errstr($exitcode), $exitsignal // 'none', $signal_names[$exitsignal];
			$Daemon{$id}->{'exitcode'} = $exitcode;
			$Daemon{$id}->{'exitsignal'} = $exitsignal;
			append_fixsizelist \@{$Daemon{$id}->{'exittimes'}}, $respawn_burst_limit, time;
			delete $Daemon{$id}->{'pid'};
			$reaped = 1;
			last;
		}
		if(not $reaped)
		{
			logger sprintf "unknown PID %d deceased, exit status %s (%s) signal %s (%s)",
				$reaped_pid, $exitcode // 'none', errstr($exitcode), $exitsignal // 'none', $signal_names[$exitsignal];
		}
	}
}

sub svlhk_dump
{
	my $objref = shift;
	my $prefix = shift;
	my $cb_ref = shift;
	
	my $type = ref $objref;
	
	if($type eq '')
	{
		$cb_ref->("$prefix	$objref\n");
	}
	elsif($type eq 'SCALAR')
	{
		$cb_ref->("$prefix	$$objref\n");
	}
	elsif($type eq 'ARRAY')
	{
		my $idx = 0;
		for my $elem (@$objref)
		{
			svlhk_dump($elem, "$prefix.$idx", $cb_ref);
			$idx++;
		}
	}
	elsif($type eq 'HASH')
	{
		for my $key (keys %$objref)
		{
			svlhk_dump($objref->{$key}, "$prefix.$key", $cb_ref);
		}
	}
	else
	{
		$cb_ref->("$prefix	$type\n");
		if($type eq 'Linux::FD::Pid')
		{
			my $fileno = $objref->fileno;
			$cb_ref->("$prefix.fileno	$fileno\n");
		}
	}
}

sub init_management_socket
{
	#my $management_socket_path = chr(0).'dmaster-management'.$ENV{'DMASTER_SOCKET_NAME_SUFFIX'};
	#my $management_socket_addr = pack('S', AF_UNIX) . $management_socket_path;
	my $management_socket_path = ($ENV{'XDG_CACHE_HOME'} || '/var/run').'/dmaster.sock';
	my $management_socket_addr = sockaddr_un($management_socket_path);
	socket(my $management_socket, AF_UNIX, SOCK_STREAM, 0) or die "socket: $!\n";
	bind($management_socket, $management_socket_addr) or die "bind: $management_socket_path: $!\n";
	chmod(0660, $management_socket_path) or die "chmod: $management_socket_path: $!\n";
	listen($management_socket, SOMAXCONN) or die "listen: $!\n";
	return $management_socket;
}


$NextAlarm_ts = 0;

sub wakeup_at_most
{
	my $sec = shift;
	my $now = time;
	my $next_alarm_ts = $now + $sec;
	if($NextAlarm_ts <= $now or $next_alarm_ts < $NextAlarm_ts)
	{
		$NextAlarm_ts = $next_alarm_ts;
		alarm $sec;
	}
}


$SIG{'HUP'} = sub { $do_reload = 1 unless $do_shutdown; };
$SIG{'CHLD'} = sub { $do_handle_children = 1; };
$SIG{'TERM'} = sub { $shutdown_signalname = $_[0]; $do_shutdown = 1; };
$SIG{'INT'} = $SIG{'TERM'};
$SIG{'ALRM'} = sub { 1; };
$SIG{'USR1'} = sub { $do_dump_state = 1; };
#$SIG{'PIPE'} = TODO


logger "init";
$management_socket = init_management_socket();
$shutdown_signalname = undef;


logger "start";
reload;

while(1)
{
	# go idle. but it will be interrupted by signals.
	my $management_client_addr = accept(my $management_client_socket, $management_socket);
	
	if(defined $management_client_addr)
	{
		print {$management_client_socket} "dmaster.pid	$$\n";
		svlhk_dump(\%Daemon, 'daemon', sub { print {$management_client_socket} $_[0]; });
		print {$management_client_socket} "END\n";
		$management_client_socket->flush;
		logger "holding on management command";
		<$management_client_socket>;
		logger "resume from management command";
		close $management_client_socket;
		undef $management_client_socket;
	}
	
	if($do_handle_children)
	{
		$do_handle_children = 0;
		handle_children;
	}
	
	if($do_shutdown)
	{
		if(not $prev_do_shutdown)
		{
			logger "got SIG$shutdown_signalname, exiting...";
			$prev_do_shutdown = $do_shutdown;
		}
		
		my $subprocesses = 0;
		for my $id (keys %Daemon)
		{
			if(exists $Daemon{$id}->{'pid'})
			{
				stop_daemon $id;
				$subprocesses++;
			}
		}
		if($subprocesses == 0)
		{
			logger "exit";
			exit;
		}
	}
	else
	{
		if($do_reload)
		{
			$do_reload = 0;
			reload;
		}
		
		# check if daemons need respawn
		for my $id (keys %Daemon)
		{
			my $dmon = $Daemon{$id};
			if(not exists $dmon->{'pid'})
			{
				if(exists $dmon->{'cmd_args'})
				{
					if(scalar @{$dmon->{'exittimes'}} >= $respawn_burst_limit and $dmon->{'exittimes'}->[0] > time - $respawn_burst_period)
					{
						my $hold_sec = $dmon->{'exittimes'}->[0] + $respawn_burst_hold - time;
						$hold_sec = 1 if $hold_sec < 1;  # alarm(0) takes int but alarm(0) means cancel
						logger "daemon $id respawned too often, hold up for $hold_sec sec";
						wakeup_at_most $hold_sec;
					}
					else
					{
						start_daemon $id;
					}
				}
				else
				{
					# it's ended and command deleted, so
					# we no longer manage this daemon
					delete $Daemon{$id};
				}
			}
		}
	}
	
	if($do_dump_state)
	{
		$do_dump_state = 0;
		my $dump_path = '/var/run/shm/dmaster.state';
		open my $fh, '>', $dump_path or warn "$dump_path: $!\n";
		if($fh)
		{
			print {$fh} Dumper \%Daemon;
			close $fh;
		}
	}
}
