#!/usr/bin/env perl

=pod

=head1 NAME

td-trans-ls - Transform ls(1) output into fix number of TAB-delimited columns.

=head1 USAGE

ls -l | td-trans-ls

=head1 DETAILS

supported ls(1) options which affect its output format:

=over 4

=item --human-readable

=item --inode

=item --recursive

=item --time-style={iso,long-iso,full-iso}

=back

unsupported options:

=over 4

=item --author

=item -g

=item -o

=item --time-style=locale

=back

=cut

do '/usr/lib/tool/perl5/tabdata/common.pl' or die "$@";

$recursive_mode = 0;
$first_line = <STDIN>;
if($first_line !~ /^\S+ \d+$/)
{
	$recursive_mode = 1;
	$first_line =~ /^(.+):$/;
	$current_directory = $1;
	# skip the "total 123" line
	<STDIN>;
}

@Headers = qw/INODE MODE LINKS OWNER GROUP SIZE MAJOR MINOR DATETIME NAME SYMLINKTARGET/;
push @Headers, 'DIRECTORY' if $recursive_mode;

print join($FS, @Headers).$/;

$mode_links_owner_group = qr/(\S+)\s+(\S+)\s+(\S+)\s+(\S+)/;

# update supported --time-style options in the documentation
$datetime = qr/((?:\d\d\d\d-)?\d\d-\d\d \d\d:\d\d(?::\d\d(?:\.\d+)? [\d+-]+)?)/;

sub parse_error
{
	die "can not parse: $_[0]\n";
}

while(<STDIN>)
{
	my @Fields;
	my $inode;
	
	if($recursive_mode)
	{
		if(/^$/)
		{
			$_ = <STDIN>;
			/^(.+):$/ or die "a directory introduction expected, not this: $_\n";
			$current_directory = $1;
			# skip the next "total 123" line
			<STDIN>;
			next;
		}
	}
	
	if(s/^(\d+)\s+//)
	{
		$inode = $1;
	}
	
	if(/^[cb]/)
	{
		@Fields = /^$mode_links_owner_group\s+()([^,]+),\s*(\S+)\s+$datetime\s+(.+)()$/ or parse_error $_;
	}
	elsif(/^l/)
	{
		@Fields = /^$mode_links_owner_group\s+(\S+)()()\s+$datetime\s+(.+?) -> (.+)$/ or parse_error $_;
	}
	else
	{
		@Fields = /^$mode_links_owner_group\s+(\S+)()()\s+$datetime\s+(.+)()$/ or parse_error $_;
	}
	
	unshift @Fields, $inode;
	push @Fields, $current_directory if $recursive_mode;
	
	print join($FS, @Fields).$RS;
}
