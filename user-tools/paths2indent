#!/usr/bin/env perl

use Getopt::Long;
use Pod::Usage;

my $do_sort = 0;
my $stop_pattern = undef;
my $DIRSEP = '/';

GetOptions(
	's|sort!' => \$do_sort,
	't|stop=s' => \$stop_pattern,
) or pod2usage(-exitval=>2, -verbose=>99);

my %parsed;

while(my $line = <STDIN>)
{
	chomp $line;
	my $ref = \%parsed;
	my ($path, $sep, $suffix);
	
	if(defined $stop_pattern)
	{
		($path, $sep, $suffix) = split /($stop_pattern)/, $line, 2;
	}
	else
	{
		$path = $line;
		$sep = $suffix = '';
	}
	
	for my $element (split /$DIRSEP/, $path)
	{
		next if $element eq '';
		$ref->{$element} = {} if not exists $ref->{$element};
		$ref = $ref->{$element};
	}
}

sub output
{
	my $indent = shift;
	my $ref = shift;
	for my $key ($do_sort ? (sort keys %$ref) : (keys %$ref))
	{
		printf "%s%s\n", "\t"x$indent, $key;
		output($indent+1, $ref->{$key});
	}
}

output(0, \%parsed);


__END__

=pod

=head1 NAME

paths2indent - Transform list of filesystem paths to an indented list of the leaf elements

=head1 SYNOPSIS

paths2indent [I<OPTIONS>]

=head1 DESCRIPTION

Input: list of file paths line-by-line

Output: leaf file names indented by as many tabs as deep the file is on the tree

=head1 OPTIONS

=over 4

=item -s, --sort

=item -t, --stop I<PATTERN>

=back

=head1 LIMITATIONS

Can not have empty path elements (ie. consecutive slashes).

=head1 SEE ALSO

=over 4

=item https://github.com/jez/as-tree

=back

=cut
