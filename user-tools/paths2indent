#!/usr/bin/env perl
# Input: list of file paths line-by-line
# Output: leaf file name with leading tabs representing the depth on the tree
#
# Similar tools:
# - https://github.com/jez/as-tree

use Getopt::Long;

my $do_sort = 0;
my $DIRSEP = '/';

GetOptions(
	's|sort!' => \$do_sort,
) or die;

my %parsed;

while(my $path = <STDIN>)
{
	my $ref = \%parsed;
	$path =~ s/\n$//;
	
	for my $element (split /$DIRSEP/, $path)
	{
		next if $element eq '';
		$ref->{$element} = {} if not exists $ref->{$element};
		$ref = $ref->{$element};
	}
}

sub output
{
	my $indent = shift;
	my $ref = shift;
	for my $key ($do_sort ? (sort keys %$ref) : (keys %$ref))
	{
		printf "%s%s\n", ' 'x$indent, $key;
		output($indent+1, $ref->{$key});
	}
}

output(0, \%parsed);


__END__

=pod

=head1 NAME

paths2indent - Transform list of filesystem paths on input into an indented list of their leaf elements

=cut
