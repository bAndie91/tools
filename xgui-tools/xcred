#!/usr/bin/env perl

use IPC::Run qw/run/;
use List::MoreUtils qw/all any none uniq/;
use Data::Dumper;
use X11::Protocol;
use URI;
use File::Slurp;
no if ($] >= 5.018), 'warnings' => 'experimental::smartmatch';


$Xorg = X11::Protocol->new();
$RootWindow = $Xorg->root;


sub esc
{
	$_[0] =~ s{[/\\]}{\\$&}gr =~ s{[\t\r\n]}{ }gr;
}

sub current_xorg_window
{
	my ($current_window_id) = $Xorg->GetInputFocus;
	my ($classes) = $Xorg->GetProperty($current_window_id, $Xorg->atom('WM_CLASS'), 0, 0, 1024);
	my ($class, $instance) = split chr(0), $classes;
	my ($title) = $Xorg->GetProperty($current_window_id, $Xorg->atom('_NET_WM_NAME'), $Xorg->atom('UTF8_STRING'), 0, 1024);
	($title) = $Xorg->GetProperty($current_window_id, $Xorg->atom('WM_NAME'), 0, 0, 1024) unless $title;
	return {id=>$current_window_id, class=>"$class.$instance", title=>$title};
}

sub extract_url
{
	my $win = current_xorg_window;
	my $url;
	if($win->{class} =~ /^navigator\.firefox/i)
	{
		my $output = '';
		run ['firefox-current-url'], '<', \undef, '>', \$url;
	}
	elsif($win->{class} =~ /^navigator\.pale/i)
	{
		# Install git://git.uucp.hu/sysop/xul-extension-current-url XUL extension in Pale Moon
		# to get this file:
		my $current_page_url_file = "$ENV{HOME}/.cache/pale moon/current_page_url";
		if(-s $current_page_url_file)
		{
			$url = read_file($current_page_url_file);
		}
		else
		{
			run ['palemoon-current-urls'], '<', \undef,
				'|', ['td-filter', 'WIN_SELECTED', '=', 'true'],
				'|', ['td-filter', 'TAB_SELECTED', '=', 'true'],
				'|', ['td-select', '-H', 'URL'], 
				'>', \$url;
		}
	}
	# put other web browsers here
	else
	{
		push @gmenu_items, "unknown foreground application: ".esc($win->{class})."\t"."header";
	}
	return $url;
}


@gmenu_items = ();
$url = extract_url;
$cred_site = undef;


if(defined $url and $url ne '')
{
	unless(eval {
		my $hostname = URI->new($url)->ihost;
		while($hostname ne '')
		{
			my $output = '';
			my $error = '';
			run ['cred', 'site', $hostname, 'dump', 'blank-secrets', 'subdirs'], \undef, \$output, \$error;
			if($output ne '')
			{
				$cred_site = $hostname;
				push @gmenu_items, "site: $cred_site"."\t"."header";
				push @gmenu_items, "site: $cred_site sep"."\t"."sep";
				for my $prop (map {[split /\t/, $_, 2]} split /\n/, $output)
				{
					my ($propname, $propvalue) = @$prop;
					$propvalue =~ s{[\\/]}{\\$&}g;
					for my $input_method (qw/clip type/)
					{
						my $label = "$propname/$input_method";
						if($propvalue ne '') { $label = "$propname: $propvalue/$input_method" ; }
						push @gmenu_items, $label ."\t". "responsetext" ."\t"."\t". "$input_method:$propname";
					}
				}
				# found a site which has properties. don't go up to wider site names:
				last;
			}
			elsif($error ne '')
			{
				push @gmenu_items, esc($error)."\t"."header";
			}
			$hostname =~ s/^[^.]+(\.|$)//;
		}
		1;
	})
	{
		push @gmenu_items, esc($@)."\t"."header";
	}
}
else
{
	push @gmenu_items, "no URL found"."\t"."header";
}


$input = join "\n", @gmenu_items;
$menu_selection = '';
run ['gmenu',], \$input, \$menu_selection;

if(defined $cred_site)
{
	chomp $menu_selection;
	my ($input_method, $propname) = $menu_selection =~ /^([^:]+):(.+)/;
	if($input_method eq 'clip')
	{
		run ['cred', 'site', $cred_site, 'clip', $propname], \undef;
	}
	elsif($input_method eq 'type')
	{
		my $propvalue = '';
		run ['cred', 'site', $cred_site, 'reveal', $propname], \undef, \$propvalue;
		run ['xdotool', 'type', '--delay', '100ms', $propvalue . "\t"];
	}
}
